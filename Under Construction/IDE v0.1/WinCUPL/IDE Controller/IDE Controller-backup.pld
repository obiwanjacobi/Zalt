Name     IDE-Controller;
PartNo   Zalt-IDE-01;
Date     27-12-2020;
Revision 01;
Designer Marc Jacobi;
Company  Canned Bytes;
Assembly Zalt IDE Board;
Location Z80 Computer / IDE;
Device   G22V10;
/*Format   j;*/

/* 
 *      IDE Interface Controller
 *
 *      CS0 (SelLatch) is synced to the (CPU)CLK (pin1) in order to 
 *      extend it beyond the IOREQ/RD/WR activity - as required by IDE timing.
 *
 */


/** input pins **/

PIN 1   = CLK;
PIN 2   = !IOREQ;
PIN 3   = !RD;
PIN 4   = !WR;
PIN 5   = A0;
PIN 6   = A1;
PIN 7   = A2;
PIN 8   = A3;
PIN 9   = A4;
PIN 10  = A5;
PIN 11  = A6;
PIN 13  = A7;


/** output pins **/

/* no output signal (OE = false) */
PIN 23  = FF;
PIN 22  = NC;
PIN 21  = SelLatch;

PIN 20  = !CS0;
PIN 19  = HiByte;
PIN 18  = EnableLo;
PIN 17  = EnableHi;
PIN 16  = !LatchLo;
PIN 15  = !IDEWR;
PIN 14  = !IDERD;


/* IDE IO Address at X0h A[3..7] */
/* A[0..2] are wired to the IDE address lines. */
IDEAddress = !A3 & A4 & A5 & A6 & A7;

/* IDE selected */
Sel = IDEAddress & IOREQ & (RD # WR);

/* Sel latched to CLK - to extend the pulse to after RD/WR have been released */
SelLatch.ar = 'b'0;
SelLatch.sp = 'b'0;
/*SelLatch.oe = 'b'0;*/     /* internal only */
SelLatch.d = Sel;

/* CS0, IDEAddress and IDERD/IDEWR all have to be asserted to activate IDE */
CS0 = SelLatch # Sel;
IDERD = Sel & RD;
IDEWR = Sel & WR;


/* Data IO Address A[0..2] = 0 */
DataAddress = !A0 & !A1 & !A2;

/* Only for Read come out of inputs */
DIR = DataAddress & IDEAddress & IOREQ & !RD;

EnableLo = !HiByte & Sel;
EnableHi = HiByte & Sel;
LatchLo = !HiByte & Sel & WR;


HiByteSet = !FF & DataAddress & IDEAddress & IOREQ;
HiByteRes = FF & IDEAddress & IOREQ;
HiByteNot = !(HiByte # HiByteSet);
HiByte = !(HiByteNot # HiByteRes);

FF.ar = 'b'0;
FF.sp = 'b'0;
/*FF.oe = 'b'0;*/
FF.d = HiByte;
